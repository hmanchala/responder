apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'eclipse-wtp'
apply plugin: 'jacoco'
apply from: 'gradle/cargo.gradle'

version = 0.1

repositories {
    maven {
    	url = 'http://maven.temenosgroup.com/content/groups/all/'
    }
}

eclipse {
	wtp {
		facet = []
		facet {
			facet name: 'java', version: '1.8'
			facet name: 'jst.web', version: '3.0'
			facet name: 'wst.jsdt.web', version: '1.0'
		}
	}
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        groovy {
            srcDirs = ['src/it/groovy']
        }
        compileClasspath += main.output + test.output + configurations.integrationTestCompile
    }
}

dependencies {
    //compilation dependencies
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.slf4j:slf4j-simple:1.7.21'
    compile 'org.glassfish.jersey.containers:jersey-container-servlet:2.24.1'
    compile 'org.codehaus.groovy:groovy-all:2.4.6'
    compile 'com.google.inject:guice:4.1.0'
    compile project(':context')
    compile project(':data')

    //test compilation dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:2.2.2'
    testCompile 'org.objenesis:objenesis:2.4'

    //integration test compilation dependencies
    integrationTestCompile 'org.glassfish.jersey.test-framework:jersey-test-framework-core:2.24.1'
    integrationTestCompile 'org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.24.1'

    //compilation dependencies available at deployment time
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    providedCompile 'javax.ws.rs:javax.ws.rs-api:2.0'
}
